" Use Vim settings, rather than Vi settings (usually better).
" This must be first, because it changes other options as a side effect.
set nocompatible

filetype off
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()

Plugin 'gmarik/vundle'
Plugin 'Valloric/YouCompleteMe'
Plugin 'scrooloose/syntastic'
Plugin 'SirVer/ultisnips'
Plugin 'christoomey/vim-tmux-navigator'

call vundle#end()

" Allow filetype detection, plugins, and indenting
filetype plugin indent on

syntax on

" The preview window is what's on top, giving more information about the
" possible completions.  This setting makes it go away when you leave insert
" mode.
let g:ycm_autoclose_preview_window_after_insertion=1

" Make UltiSnips play nicely with YCM.  These mappings do not work well in
" latex, and I can't figure out how to change <leader> to something other than
" '\' (let mapleader = '\,' doesn't work, probably because of when plugins get
" loaded).  So we'll just suffer through latex editing for now...
let g:UltiSnipsExpandTrigger = '<leader>f'
let g:UltiSnipsJumpForwardTrigger = '<leader>g'
let g:UltiSnipsJumpBackwardTrigger = '<leader>d'

" This makes eclim play nicely with YCM.
let g:EclimCompletionMethod = 'omnifunc'

" This makes it so that eclim doesn't show TODOs as errors on the left side of
" the screen, as TODOs in eclipse come in at "sign level" 3 (task tags).  I
" wish there were a better way to do this, as some other warnings are quite
" helpful...
let g:EclimSignLevel = 2

" Some Eclim shortcuts
au FileType java nmap <leader>jc :JavaCorrect<cr>
au FileType java nmap <leader>js :JavaSearchContext<cr>
au FileType java nmap <leader>ji :JavaImport<cr>
au FileType java nmap <leader>jio :JavaImportOrganize<cr>

" Eclim disables syntastic for java files, anyway, so there's not a lot of
" point in having this anymore.  But maybe I'll leave it as an example in case
" I ever want to disable a filetype again.
"let g:syntastic_mode_map = { 'mode': 'active',
                           "\ 'active_filetypes': [],
                           "\ 'passive_filetypes': ['java'] }

" Not sure what the deal is, but YCM messed up backspace, and this fixes it
set backspace=indent,eol,start

" Show line numbers
set number

" Trying to get auto-indentation to be more like Google's style guide...
set cinoptions+=(0,w1

" When editing a file, always jump to the last curser position
autocmd BufReadPost *
\ if line("'\"") > 0 && line ("'\"") <= line("$") |
\   exe "normal! g'\""  |
\ endif
" But don't do that when editing a git commit message - go back to the
" first line
autocmd BufReadPost *COMMIT_EDITMSG exe "normal 1G"

" Hit F4 while in insert mode to do a correct paste
set pastetoggle=<F4>

" Associate *.tape files with ducttape
au BufRead,BufNewFile *.tape set filetype=ducttape

" Some python goodies
autocmd FileType python set foldlevel=1
autocmd FileType python set includeexpr=PythonIncludeExpr()
autocmd FileType python set include=^\\(from\\\\|import\\)
autocmd FileType python set path+=,~/pythonutil

" For python you want 4 spaces to be a tab, and tabs should be expanded
autocmd FileType python setlocal et sw=4 sts=4

" You know what, just use spaces instead of tabs everywhere
set et sw=4 sts=4

" Except, lets start moving towards two spaces instead of four, to save on line
" width
autocmd FileType htmldjango setlocal et sw=2 sts=2
autocmd FileType javascript setlocal et sw=2 sts=2
autocmd FileType tex setlocal et sw=2 sts=2


" Enforce 80 character line widths by wrapping automatically
" And I like having an empty space on the end, so we actually make it 79
set textwidth=79

" Let lines in java be 100 characters long, as per convention
autocmd FileType java set textwidth=99

" HTML files can have pretty long lines, let's say.
autocmd FileType htmldjango set textwidth=139

" Highlight the 81st column, if possible
if exists('+colorcolumn')
    set colorcolumn=81
    autocmd FileType java set colorcolumn=101
    autocmd FileType htmldjango set colorcolumn=141
" If it isn't, highlight any characters that go over the allowed width.
else
    au Filetype python,c,cpp,tex let w:m2=matchadd('ErrorMsg', '\%>80v.\+', -1)
    au Filetype java let w:m2=matchadd('ErrorMsg', '\%>100v.\+', -1)
    au Filetype htmldjango let w:m2=matchadd('ErrorMsg', '\%>140v.\+', -1)
endif

" And put an underline on the line where the cursor currently is, to save on
" searching for the cursor when switching windows.
set cursorline

" Use syntax folding for C++ because I don't have a good script for it
autocmd FileType cpp set foldmethod=syntax

" Use the skeletons when opening python and latex files
autocmd BufNewFile *.py r ~/.vim/skel.py
autocmd BufNewFile *.tex r ~/.vim/skel.tex

" Gets rid of an extra line that gets put in
autocmd BufNewFile *.py exe "normal 1Gddj"
autocmd BufNewFile *.tex exe "normal 1Gddj"

" Use django file type for html files, 'cause I don't do any other html
autocmd BufEnter *.html setfiletype htmldjango

" Include bibliography files for autocompletion purposes
autocmd FileType tex set include=\\\\input\\\\|\\\\include{\\\\|\\\\bibliography{\\\\|\\.\\.\\/\\.\\.\\/\\.\\.
autocmd FileType tex set suffixesadd=.tex,.bib
function! LatexIncludeExpr(fname)
    let fname = a:fname
    if fname[0] != "."
        let fname = "../../.." . fname
    endif
    let fname = substitute(fname, '^.\{-}{\|}.*\|,.*', '', 'g')
    return fname
endfunction
autocmd FileType tex let &l:includeexpr="LatexIncludeExpr(v:fname)"

" Add a highlight group to look out for trailing whitespace.
" Thanks to: http://vim.wikia.com/wiki/Highlight_unwanted_spaces
highlight TrailingWhitespace ctermbg=red guibg=red
autocmd colorscheme * highlight TrailingWhitespace ctermbg=red guibg=red
2match TrailingWhitespace /\s\+$/
" Explanation of the following: whitespace: \s , one or more: \+ ,
"   current cursor position: \%# , negate: \@<! , end of line: $
autocmd InsertEnter * 2match TrailingWhitespace /\s\+\%#\@<!$/
autocmd InsertLeave * 2match TrailingWhitespace /\s\+$/
autocmd BufWinEnter * 2match TrailingWhitespace /\s\+$/
" The following apparently avoids a memory leak.
autocmd BufWinLeave * call clearmatches()

" Also highlight tab characters, which are evil.
set list
set listchars=tab:\ \ ,
highlight SpecialKey ctermbg=Red guibg=Red

function! PythonIncludeExpr()
  "echo v:fname
  for dir in split(&path, ',')
    "echo dir
    if isdirectory(dir . '/' . v:fname)
      echo 'Directory found'
      return v:fname . "/__init__.py"
    endif
  endfor
  return substitute(v:fname, '\.', '/', 'g')
endfunction

" Add everything in PYTHONPATH to vim's path, for autocompletion purposes
"python << EOF
"import os
"import sys
"import vim
"for p in sys.path:
  "# Add each directory in sys.path, if it exists.
  "if os.path.isdir(p):
    "# Command 'set' needs backslash before each space.
    "vim.command(r"set path+=%s" % (p.replace(" ", r"\ ")))
"EOF

" Automatically format comments when typing.  So if you type a long comment,
" vim will autowrap for you.
set formatoptions+=r
" Also continue a comment if inserting with 'O' directly under or over an
" existing comment.
set formatoptions+=o

" Get rid of the annoying default plaintex for empty .tex files
let g:tex_flavor='latex'

" Add included files to autocomplete
set complete+=i

" Make a mapping to do spellchecking
nmap <silent> <F3> :setlocal spell spelllang=en_us<cr>

" Allow lower case to match upper case search targets.
set smartcase

" Always show the status line
set laststatus=2

" Add syntastic stuff to the status line
"set statusline+=%#warningmsg#
"set statusline+=%{SyntasticStatuslineFlag()}
"set statusline+=%*

" Show partial commands in the command buffer as they are being typed
set showcmd

" ReST editing stuff
function ReST_Extend_Line()
    let cur = getline(".")
    let prev = getline( line(".") - 1 )
    return repeat(cur[0], len(prev) - len(cur))
endfunction
imap <F9> <c-r>=ReST_Extend_Line()<cr>
map <F9> yypVr

" Don't use Ex mode.
map Q <nop>

" Get rid of annoying ctr-u in insert mode
imap <c-u> <nop>

" Tab completion for ex command-line (like :split)
set wildmenu
set wildmode=longest:full,full

" When at the top or bottom of the terminal, always leave 2 lines above the
" curser
set scrolloff=2

" Some folding options
set foldenable
set foldlevelstart=0
set foldopen=block,insert,hor,jump,mark,percent,quickfix,search,tag,undo

" Maps F2 to getting rid of the highlighting after a search - very nice
nmap <silent> <F2> :nohl<cr>:set spelllang=<cr>

" Maps F5 to cleaning up whitespace - convert tab characters to 4 spaces and
" remove any trailing whitespace.
nmap <F5> :%s/\s\+$//<cr>:%s/\t/    /g<cr>

" Easier window resizing.
nmap + <C-W>+
nmap _ <C-W>-
nmap <C-H> <C-W><
nmap <C-L> <C-W>>

" Use <C-T> to run :TlistToggle - the taglist plugin that shows code structure
nmap <C-T> :TlistToggle<cr>

set hlsearch " Highlight the last used search pattern
set ruler " show the cursor position all the time

